buildscript {

    ext.kotlin_version = "1.3.72"
    ext.build_gradle_version = '3.6.3'
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public/' } //jcenter和central
        maven { url 'https://maven.aliyun.com/repository/google/' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin/' }
        maven { url 'https://maven.fabric.io/public' }
        google()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$build_gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        maven { url 'https://maven.aliyun.com/repository/public/' } //jcenter和central
        maven { url 'https://maven.aliyun.com/repository/google/' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin/' }
        maven { url 'https://dl.bintray.com/ppartisan/maven/' }
        maven { url "https://clojars.org/repo/" }
        maven { url "https://jitpack.io" }
        mavenLocal()
        mavenCentral()
        google()
    }
}


task clean(type: Delete) {
    delete rootProject.buildDir
}

task restartTaoKDao(){
    doLast{
        Runtime.getRuntime().exec("adb shell am force-stop tiiehenry.taokdao").waitFor()
        Runtime.getRuntime().exec("adb shell am start -S -n tiiehenry.taokdao/taokdao.main.MainActivity")
    }
}

gradle.projectsEvaluated {
    subprojects { project ->
        if (project.version != "unspecified") {
            println project.group + ":" + project.name + ":" + project.version
            apply plugin: 'maven'
            // 获取本地仓库路径
            def localRepositoryPath = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
            //打包至本地仓库配置
            uploadArchives {
                repositories {
                    //mavenDeployer 需要依赖 apply plugin: 'maven'
                    mavenDeployer {
                        repository(url: localRepositoryPath)
                        pom.project {
                            name = project.name        // 当前项目名称
                            version = project.version   //当前项目版本  0.0.1
                            groupId = project.group     //  当前项目组  com.xxx
                        }
                    }
                }
            }
        }
    }
}